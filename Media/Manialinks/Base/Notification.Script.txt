#Struct SNotification {
	Text Name;
	Text Title;
	Text Content;
	Text Type;
	Text Date;
}

declare Boolean IsOpen;
declare Integer OpenStartTime;
declare Integer OpenEndTime;
declare Integer PrevNotificationCount;
declare Boolean Fully;

Void Open() {
	AnimMgr.Add((Page.GetFirstChild("FRAME_BG") as CMlFrame), "<frame pos=\"0 5\"/>", 200, CAnimManager::EAnimManagerEasing::QuadOut);
	foreach(Bg, (Page.GetFirstChild("FRAME_BG") as CMlFrame).Controls) {
		AnimMgr.Add(Bg, "<quad size=\"10 20\" pos=\"0 -5\"/>", 200, CAnimManager::EAnimManagerEasing::QuadOut);
		AnimMgr.AddChain(Bg, "<quad size=\"60 20\"/>", 200, CAnimManager::EAnimManagerEasing::QuadIn);
	}
	IsOpen = True;
	OpenStartTime = Now;
	OpenEndTime = -1;
	
	declare SNotification[] TempNotifications for LocalUser;

	Page.GetFirstChild("FRAME_OLDER_NOTIFICATIONS").RelativePosition_V3.X = 0.;
	if(Fully) AnimMgr.Add(Page.GetFirstChild("FRAME_OLDER_NOTIFICATIONS"), "<frame pos=\"0 "^(100-MathLib::Clamp(TempNotifications.count-1,0,9)*10+.1)^"\"/>", Now+400, 500, CAnimManager::EAnimManagerEasing::QuadOut);
}

Void Close() {
	AnimMgr.Add((Page.GetFirstChild("FRAME_BG") as CMlFrame), "<frame pos=\"0 0\"/>", Now+200, 200, CAnimManager::EAnimManagerEasing::QuadIn);
	foreach(Bg, (Page.GetFirstChild("FRAME_BG") as CMlFrame).Controls) {
		AnimMgr.Add(Bg, "<quad size=\"10 20\"/>", 200, CAnimManager::EAnimManagerEasing::QuadOut);
		AnimMgr.AddChain(Bg, "<quad size=\"10 10\"/>", 200, CAnimManager::EAnimManagerEasing::QuadIn);
	}
	AnimMgr.Add(Page.GetFirstChild("FRAME_OLDER_NOTIFICATIONS"), "<frame pos=\"55 100.1\"/>", 500, CAnimManager::EAnimManagerEasing::QuadOut);

	IsOpen = False;
	OpenStartTime = -1;
	OpenEndTime = Now;
}

Void UpdateNotifications() {
	declare Frame_Notification = (Page.GetFirstChild("FRAME_NOTIFICATION") as CMlFrame);

	declare SNotification[] TempNotifications for LocalUser;

	if(TempNotifications.count > 0) {
		declare Notification = TempNotifications[0];

		switch(Notification.Type) {
			case "Ban": {
				(Page.GetFirstChild("QUAD_COLOR") as CMlQuad).Colorize = <1.,0.,0.>;
				(Page.GetFirstChild("LABEL_TITLE") as CMlLabel).SetText("{{{{YOU_HAVE_BEEN_BANNED}}}}");
				(Page.GetFirstChild("LABEL_SHORTDESCRIPTION") as CMlLabel).SetText("{{{{REASON}}}}: "^Notification.Content ^ " ({{{{kicking_in_5_seconds}}}}) ");
			}
			case "CriticalUpdate": {
				(Page.GetFirstChild("QUAD_COLOR") as CMlQuad).Colorize = <1.,0.,0.>;
				(Page.GetFirstChild("LABEL_TITLE") as CMlLabel).SetText("{{{{CRITICAL_UPDATE}}}}");
				(Page.GetFirstChild("LABEL_SHORTDESCRIPTION") as CMlLabel).SetText("{{{{ACCEPT_CRITICAL_UPDATE}}}} ({{{{kicking_in_5_seconds}}}}) ");
			}
			case "Update": {
				(Page.GetFirstChild("QUAD_COLOR") as CMlQuad).Colorize = <1.,.9,.0>;
				(Page.GetFirstChild("LABEL_TITLE") as CMlLabel).SetText("{{{{NEW_UPDATE}}}}");
				(Page.GetFirstChild("LABEL_SHORTDESCRIPTION") as CMlLabel).SetText("$o"^Notification.Title^":$z "^Notification.Content ^ " ");
			}
			case "Shutdown": {
				(Page.GetFirstChild("QUAD_COLOR") as CMlQuad).Colorize = <1.,.6,.0>;
				(Page.GetFirstChild("LABEL_TITLE") as CMlLabel).SetText("{{{{SERVICE_SHUTDOWN}}}}");
				(Page.GetFirstChild("LABEL_SHORTDESCRIPTION") as CMlLabel).SetText(Notification.Content ^ " ");
			}
			case "Moderator": {
				(Page.GetFirstChild("QUAD_COLOR") as CMlQuad).Colorize = <0.,.8,.0>;
				(Page.GetFirstChild("LABEL_TITLE") as CMlLabel).SetText("MODERATOR {{{{SAYS_TO_YOU}}}}...");
				(Page.GetFirstChild("LABEL_SHORTDESCRIPTION") as CMlLabel).SetText("\""^Notification.Content ^ "\" ");
			}
			case "Achievement": {
				(Page.GetFirstChild("QUAD_COLOR") as CMlQuad).Colorize = <.0,.0,1.>;
				(Page.GetFirstChild("LABEL_TITLE") as CMlLabel).SetText(TextLib::ToUpperCase(Notification.Title));
				(Page.GetFirstChild("LABEL_SHORTDESCRIPTION") as CMlLabel).SetText(Notification.Content ^ " ");
			}
			default: {
				(Page.GetFirstChild("QUAD_COLOR") as CMlQuad).Colorize = <.0,.0,.0>;
				(Page.GetFirstChild("LABEL_TITLE") as CMlLabel).SetText(TextLib::ToUpperCase(Notification.Title));
				(Page.GetFirstChild("LABEL_SHORTDESCRIPTION") as CMlLabel).SetText(Notification.Content ^ " ");
			}
		}
	}
	else {
		(Page.GetFirstChild("LABEL_TITLE") as CMlLabel).SetText("");
		(Page.GetFirstChild("LABEL_SHORTDESCRIPTION") as CMlLabel).SetText("");
		(Page.GetFirstChild("QUAD_COLOR") as CMlQuad).Colorize = <.0,.0,.0>;

		if(IsOpen)
			Close();
	}

	declare Frame_Older_Notification = (Page.GetFirstChild("FRAME_OLDER_NOTIFICATIONS") as CMlFrame);

	for(I, 0, Frame_Older_Notification.Controls.count-1) {
		if(TempNotifications.existskey(I) && Frame_Older_Notification.Controls.existskey(I)) {
			declare Frame_MiniNotification = (Frame_Older_Notification.Controls[I+MathLib::Max(0,Frame_Older_Notification.Controls.count-TempNotifications.count)] as CMlFrame);
			declare Notification = TempNotifications[I];
			Frame_MiniNotification.DataAttributeSet("id", I^"");
			
			switch(Notification.Type) {
				case "Ban": {
					(Frame_MiniNotification.GetFirstChild("QUAD_COLOR") as CMlQuad).Colorize = <1.,0.,0.>;
					(Frame_MiniNotification.GetFirstChild("LABEL_TITLE") as CMlLabel).SetText("{{{{YOU_HAVE_BEEN_BANNED}}}}");
				}
				case "CriticalUpdate": {
					(Frame_MiniNotification.GetFirstChild("QUAD_COLOR") as CMlQuad).Colorize = <1.,0.,0.>;
					(Frame_MiniNotification.GetFirstChild("LABEL_TITLE") as CMlLabel).SetText("{{{{CRITICAL_UPDATE}}}}");
				}
				case "Update": {
					(Frame_MiniNotification.GetFirstChild("QUAD_COLOR") as CMlQuad).Colorize = <1.,.9,.0>;
					(Frame_MiniNotification.GetFirstChild("LABEL_TITLE") as CMlLabel).SetText("{{{{NEW_UPDATE}}}}");
				}
				case "Shutdown": {
					(Frame_MiniNotification.GetFirstChild("QUAD_COLOR") as CMlQuad).Colorize = <1.,.6,.0>;
					(Frame_MiniNotification.GetFirstChild("LABEL_TITLE") as CMlLabel).SetText("{{{{SERVICE_SHUTDOWN}}}}");
				}
				case "Moderator": {
					(Frame_MiniNotification.GetFirstChild("QUAD_COLOR") as CMlQuad).Colorize = <0.,.8,.0>;
					(Frame_MiniNotification.GetFirstChild("LABEL_TITLE") as CMlLabel).SetText("MODERATOR {{{{SAYS_TO_YOU}}}}...");
				}
				case "Achievement": {
					(Frame_MiniNotification.GetFirstChild("QUAD_COLOR") as CMlQuad).Colorize = <.0,.0,1.>;
					(Frame_MiniNotification.GetFirstChild("LABEL_TITLE") as CMlLabel).SetText(TextLib::ToUpperCase(Notification.Title));
				}
				default: {
					(Frame_MiniNotification.GetFirstChild("QUAD_COLOR") as CMlQuad).Colorize = <.0,.0,.0>;
					(Frame_MiniNotification.GetFirstChild("LABEL_TITLE") as CMlLabel).SetText(TextLib::ToUpperCase(Notification.Title));
				}
			}
		}
	}

	if(Fully) AnimMgr.Add(Page.GetFirstChild("FRAME_OLDER_NOTIFICATIONS"), "<frame pos=\"0 "^(100-MathLib::Clamp(TempNotifications.count-1,0,9)*10+.1)^"\"/>", 500, CAnimManager::EAnimManagerEasing::QuadOut);
}

***Start***
***
foreach(Bg, (Page.GetFirstChild("FRAME_BG") as CMlFrame).Controls) {
	declare Quad = (Bg as CMlQuad);
	Quad.Size.X = 10.;
	Quad.Size.Y = 10.;
	Quad.RelativePosition_V3.Y = -5.;
}

IsOpen = False;
OpenStartTime = -1;
OpenEndTime = -1;

declare NotificationTime = -1;

UpdateNotifications();
***

***Loop***
***
foreach(Event,PendingEvents) {
	switch(Event.Type) {
		case CMlScriptEvent::Type::MouseClick: {
			if(Event.ControlId == "QUAD_INFO") {
				if(IsOpen) {
					Close();
				}
				else {
					Fully = True;
					Open();
				}

				NotificationTime = -1;
			}
			if(Event.ControlId == "QUAD_CLOSE") {
				declare SNotification[] TempNotifications for LocalUser;
				if(Event.Control.Parent.Parent.Parent.ControlId == "FRAME_FIRST_NOTIFICATION") {
					if(TempNotifications.count > 0) TempNotifications.removekey(0);
					NotificationTime = -1;
				}
				else {
					declare Id = TextLib::ToInteger(Event.Control.Parent.Parent.Parent.DataAttributeGet("id"));
					if(TempNotifications.count > Id) TempNotifications.removekey(Id);
				}
				UpdateNotifications();
			}
		}
		case CMlScriptEvent::Type::PluginCustomEvent: {
			switch(Event.CustomEventType) {
				case "Show": {
					
				}
				case "UpdateNotifications": {
					Fully = False;
					UpdateNotifications();
				}
				case "Notification": {
					NotificationTime = Now;

					Fully = False;
					UpdateNotifications();

					Open();
				}
				case "Position": {
					declare Location = TextLib::Split(" ", Event.CustomEventData[0]);
					AnimMgr.Add(Page.GetFirstChild("FRAME_NOTIFICATION"), "<frame pos=\""^Event.CustomEventData[0]^"\"/>", 500, CAnimManager::EAnimManagerEasing::QuadOut);
				}
			}
		}
	}
}

if(OpenStartTime != -1 && OpenEndTime == -1) {
	(Page.GetFirstChild("FRAME_INNER_NOTIFICATION") as CMlFrame).ClipWindowRelativePosition.Y = AnimLib::EaseOutQuad(Now - OpenStartTime, -5., 5., 200);
	(Page.GetFirstChild("FRAME_INNER_NOTIFICATION") as CMlFrame).ClipWindowSize.Y = AnimLib::EaseOutQuad(Now - OpenStartTime, 10., 10., 200);

	(Page.GetFirstChild("FRAME_INNER_NOTIFICATION") as CMlFrame).ClipWindowRelativePosition.X = AnimLib::EaseInQuad(Now - OpenStartTime-200, -5., -25., 200);
	(Page.GetFirstChild("FRAME_INNER_NOTIFICATION") as CMlFrame).ClipWindowSize.X = AnimLib::EaseInQuad(Now - OpenStartTime-200, 10., 50., 200);
}
if(OpenEndTime != -1 && OpenStartTime == -1) {
	(Page.GetFirstChild("FRAME_INNER_NOTIFICATION") as CMlFrame).ClipWindowRelativePosition.X = AnimLib::EaseOutQuad(Now - OpenEndTime, -30., 25., 200);
	(Page.GetFirstChild("FRAME_INNER_NOTIFICATION") as CMlFrame).ClipWindowSize.X = AnimLib::EaseOutQuad(Now - OpenEndTime, 60., -50., 200);

	(Page.GetFirstChild("FRAME_INNER_NOTIFICATION") as CMlFrame).ClipWindowRelativePosition.Y = AnimLib::EaseInQuad(Now - OpenEndTime-200, 0., -5., 200);
	(Page.GetFirstChild("FRAME_INNER_NOTIFICATION") as CMlFrame).ClipWindowSize.Y = AnimLib::EaseInQuad(Now - OpenEndTime-200, 20., -10., 200);
}

if(NotificationTime != -1 && Now - NotificationTime > 5*1000) {
	NotificationTime = -1;
	Close();
}
***