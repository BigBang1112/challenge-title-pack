#Struct SStunt {
	Integer Timestamp;
	Integer Points;
	Text Figure;
	Integer Angle;
	Integer RaceTime;
	Integer Quality;
}

CTmMlPlayer GetOwner() {
	wait(GUIPlayer != Null || InputPlayer != Null);
	if(GUIPlayer != Null) return GUIPlayer;
	return InputPlayer;
}

***Start***
***
declare Label_Points = (Page.GetFirstChild("LABEL_POINTS") as CMlLabel);
declare Label_Stunt = (Page.GetFirstChild("LABEL_STUNT") as CMlLabel);
declare Label_TimePenalty = (Page.GetFirstChild("LABEL_TIMEPENALTY") as CMlLabel);

declare TotalPoints = 0;

declare CTmMlPlayer::ERaceState PreviousRaceState;

declare metadata Integer StuntTimeLimit for Map;
***

***Loop***
***
foreach(Event,PendingEvents) {
	switch(Event.Type) {
		case CMlScriptEvent::Type::MouseClick: {
			
		}
		case CMlScriptEvent::Type::PluginCustomEvent: {
			switch(Event.CustomEventType) {
				case "Start": {
					
				}
				case "Stunt": {
					declare SStunt Stunt;
					declare JsonSuccess = Stunt.fromjson(Event.CustomEventData[0]);
					
					if(Stunt.Quality > 0) {
						TotalPoints += Stunt.Points;

						if(Stunt.Angle == 0)
							Label_Stunt.SetText(Stunt.Figure);
						else
							Label_Stunt.SetText(Stunt.Figure ^ " " ^ Stunt.Angle);
					}
					else if(Stunt.Quality == 0) TotalPoints += Stunt.Points;
					else if(Stunt.Quality < 0) {
						TotalPoints -= Stunt.Points;
						Label_Stunt.SetText(Stunt.Figure);
					}
				}
			}
		}
	}
}

if(StuntTimeLimit == 0) {
	Label_TimePenalty.SetText("-:--.--");
}
else if(GetOwner().RaceStartTime == 0) {
	Label_TimePenalty.SetText(TextLib::TimeToText(StuntTimeLimit, True));
}
else {
	declare Time = MathLib::Clamp(StuntTimeLimit-(GameTime-GetOwner().RaceStartTime),0,StuntTimeLimit);
	Label_TimePenalty.SetText(TextLib::TimeToText(Time, True));
}

foreach(Event,RaceEvents) {
	switch(Event.Type) {
		case CTmRaceClientEvent::EType::Respawn: {
			
		}
	}
}

if(GetOwner().RaceState != PreviousRaceState) {
	declare State = GetOwner().RaceState;
	if(State == CTmMlPlayer::ERaceState::BeforeStart) {
		TotalPoints = 0;
		Label_Stunt.SetText("");
	}
	PreviousRaceState = State;
}

Label_Points.SetText(TotalPoints^"");
***
