***Start***
***
declare Start = Now;
declare End = -1;
***

***Loop***
***
foreach(Event,PendingEvents) {
	switch(Event.Type) {
		case CMlScriptEvent::Type::MouseClick: {
			switch(Event.ControlId) {
				case "": {
					
				}
			}
		}
		case CMlScriptEvent::Type::PluginCustomEvent: {
			switch(Event.CustomEventType) {
				case "Show": {
					Start = Now;
					End = -1;

					Page.GetFirstChild("FRAME_NEWS").RelativePosition_V3 = <-115.,7.5>;
					AnimMgr.Add(Page.GetFirstChild("FRAME_NEWS"), "<frame pos=\"-115 35\"/>", Now + 500 + 500, 500, CAnimManager::EAnimManagerEasing::QuadOut);

					foreach(Control,(Page.GetFirstChild("FRAME_BACKGROUND") as CMlFrame).Controls) {
						(Control as CMlQuad).Opacity = 0.;
						AnimMgr.Add(Control, "<quad opacity=\"1\"/>", Now + 500, 500, CAnimManager::EAnimManagerEasing::QuadOut);
					}

					(Page.GetFirstChild("LABEL_NEWS") as CMlLabel).Opacity = 0.;
					AnimMgr.Add(Page.GetFirstChild("LABEL_NEWS"), "<label opacity=\"1\"/>", Now + 500, 500, CAnimManager::EAnimManagerEasing::QuadOut);

					declare Delay = 0;
					foreach(Control,(Page.GetFirstChild("FRAME_LIST") as CMlFrame).Controls) {
						Control.Hide();
						AnimMgr.Add(Control, "<frame hidden=\"0\"/>", Now + 500 + 500 + Delay, 500, CAnimManager::EAnimManagerEasing::QuadOut);
						Delay += 100;
					}
				}
				case "Hide": {
					End = Now;
					AnimMgr.Add(Page.GetFirstChild("FRAME_NEWS"), "<frame pos=\"-200 35\"/>", 500, CAnimManager::EAnimManagerEasing::QuadOut);
				}
			}
		}
	}
}

declare Time = Now - Start - 1000;

if(Time <= 1500) {
	foreach(Control,(Page.GetFirstChild("FRAME_BACKGROUND") as CMlFrame).Controls) {
		Control.Size.Y = AnimLib::EaseOutQuad(Time, 15.0, 55.0, 500);
	}
	Page.GetFirstChild("QUAD_SCROLLBAR_BACKGROUND").Size.Y = AnimLib::EaseOutQuad(Time, 0.0, 50.0, 500);
	Page.GetFirstChild("QUAD_LIST_BG").Size.Y = AnimLib::EaseOutQuad(Time, 0.0, 50.0, 500);
	Page.GetFirstChild("QUAD_SCROLLBAR").Size.Y = AnimLib::EaseOutQuad(Time, 0.0, 20.0, 500);
}

if(End != -1) {
	if(Now - End > 1000) {
		SendCustomEvent("Hide_Response", []);
		End = -1;
	}
}
***